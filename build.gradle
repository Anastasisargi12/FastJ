plugins {
    // it's a java library. What did you expect?
    id 'java-library'

    // jlink and jpackage plugins are always nice
    id 'org.beryx.jlink' version '2.23.1'
}

group('io.github.lucasstarsz.fastj')
version('1.0.0')

sourceSets {
    game {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output

        task game(type: JavaExec) {
            description('A game in FastJ.')
            classpath = files(sourceSets.main.output, sourceSets.game.runtimeClasspath)
            main = 'io.github.lucasstarsz.fastj.game.Main'
        }
    }
}

/* Java modules need this in order for the module path to be inferred based on module-info.java
 * files. */
plugins.withType(JavaPlugin).configureEach {
    java {
        modularity.inferModulePath = true
    }
}

jlink {

    options.addAll('--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages')

    launcher {
        name = 'FastJ Game'
        noConsole = true
    }

    jpackage {
        // cross-platform options
        jvmArgs += ['-XX:+CreateCoredumpOnCrash']
        installerOptions += [
                '--description', 'A game made using FastJ.',
                '--vendor', 'Andrew Dey',
                '--copyright', 'CopyrightÂ© 2021 Andrew Dey',
//                '--license-file', 'src/main/resources/LICENSE.md',
                '--app-version', project.version
        ]

        // platform-specific options
        def currentOs = org.gradle.internal.os.OperatingSystem.current()

        if (currentOs.windows) {
            installerType = "msi"

            installerOptions += [
                    '--win-per-user-install',
                    '--install-dir', 'FastJ-Game',
                    '--win-dir-chooser',
                    '--win-shortcut'
            ]
        } else if (currentOs.linux) {
            installerType = "deb"

            installerOptions += [
                    '--linux-shortcut',
                    '--linux-deb-maintainer', 'andrewrcdey@gmail.com'
            ]
        } else if (currentOs.macOsX) {
            installerType = "pkg"

            installerOptions += [
                    '--mac-package-name', 'FastJ Game'
            ]
        }
    }
}
